
FUNCTION_BLOCK utilityAspect	

VAR_INPUT
	vmFormation : REAL;
	runtime : REAL;
	sla : REAL;
	ipReaction : REAL;
	legal : REAL;
END_VAR

VAR_OUTPUT
	trust : REAL;
END_VAR

FUZZIFY vmFormation			
	TERM tooPoor := (0, 1) (0.71, 1) (1.07, 0) ; 
	TERM poor := (0.71, 0) (1.07, 1) (1.78, 1) (2.14, 0) ;
	TERM medium := (1.78, 0) (2.14, 1) (2.85, 1) (3.21, 0) ;
	TERM good := (2.85, 0) (3.21, 1) (3.92, 1) (4.28, 0) ;
	TERM excellent := (3.92, 0) (4.28, 1) (5.0, 1) ; 
END_FUZZIFY

FUZZIFY runtime			
	TERM tooLow := (0, 1) (0.71, 1) (1.07, 0) ; 
	TERM low := (0.71, 0) (1.07, 1) (1.78, 1) (2.14, 0) ;
	TERM medium := (1.78, 0) (2.14, 1) (2.85, 1) (3.21, 0) ;
	TERM high := (2.85, 0) (3.21, 1) (3.92, 1) (4.28, 0) ;
	TERM veryHigh := (3.92, 0) (4.28, 1) (5.0, 1) ; 
END_FUZZIFY

FUZZIFY sla			
	TERM veryFew := (0, 1) (0.71, 1) (1.07, 0) ; 
	TERM few := (0.71, 0) (1.07, 1) (1.78, 1) (2.14, 0) ;
	TERM many := (1.78, 0) (2.14, 1) (2.85, 1) (3.21, 0) ;
	TERM most := (2.85, 0) (3.21, 1) (3.92, 1) (4.28, 0) ;
	TERM always := (3.92, 0) (4.28, 1) (5.0, 1) ; 
END_FUZZIFY

FUZZIFY ipReaction			
	TERM tooSlow := (0, 1) (0.71, 1) (1.07, 0) ; 
	TERM slow := (0.71, 0) (1.07, 1) (1.78, 1) (2.14, 0) ;
	TERM medium := (1.78, 0) (2.14, 1) (2.85, 1) (3.21, 0) ;
	TERM fast := (2.85, 0) (3.21, 1) (3.92, 1) (4.28, 0) ;
	TERM veryFast := (3.92, 0) (4.28, 1) (5.0, 1) ; 
END_FUZZIFY

FUZZIFY legal			
	TERM tooLow := (0, 1) (0.71, 1) (1.07, 0) ; 
	TERM low := (0.71, 0) (1.07, 1) (1.78, 1) (2.14, 0) ;
	TERM medium := (1.78, 0) (2.14, 1) (2.85, 1) (3.21, 0) ;
	TERM high := (2.85, 0) (3.21, 1) (3.92, 1) (4.28, 0) ;
	TERM veryHigh := (3.92, 0) (4.28, 1) (5.0, 1) ; 
END_FUZZIFY

DEFUZZIFY trust
	TERM veryLow := (0, 1) (0.71, 1) (1.07, 0) ; 
	TERM low := (0.71, 0) (1.07, 1) (1.78, 1) (2.14, 0) ;
	TERM medium := (1.78, 0) (2.14, 1) (2.85, 1) (3.21, 0) ;
	TERM high := (2.85, 0) (3.21, 1) (3.92, 1) (4.28, 0) ;
	TERM veryHigh := (3.92, 0) (4.28, 1) (5.0, 1) ;
	METHOD : COG;		// Use 'Center Of Gravity' defuzzification method
	DEFAULT := 2.5;		// Default value is 2.5 (if no rule activates defuzzifier)
END_DEFUZZIFY

RULEBLOCK No1
	AND : MIN;			// Use 'min' for 'and' (also implicit use 'max' for 'or')
	ACT : MIN;			// Use 'min' activation method
	ACCU : MAX;			// Use 'max' accumulation method

	RULE 1 : IF sla IS always AND vmFormation IS excellent AND runtime IS veryHigh AND legal is veryHigh THEN trust IS veryHigh;
	RULE 2 : IF sla IS veryFew OR legal IS tooLow THEN trust IS veryLow;
	RULE 3 : IF ipReaction IS NOT tooSlow AND (vmFormation IS excellent OR runtime IS veryHigh) THEN trust IS veryHigh;
	RULE 4 : IF sla IS most AND (vmFormation IS good AND legal IS high) THEN trust IS high;
	RULE 5 : IF legal IS low AND (sla IS NOT always OR sla IS NOT most) THEN trust IS low;
	RULE 6 : IF ipReaction IS fast OR (vmFormation IS NOT poor AND runtime IS medium AND sla IS NOT few) THEN trust IS medium;	
END_RULEBLOCK

END_FUNCTION_BLOCK

